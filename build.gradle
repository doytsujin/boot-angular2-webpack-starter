buildscript {
	ext {
		springBootVersion = '1.3.4.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.moowork.gradle:gradle-node-plugin:0.12"
	}
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'com.moowork.node'

jar {
	baseName = 'app'
	version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}


dependencies {
	compile('org.springframework.cloud:spring-cloud-starter-config')
	compile('org.springframework.boot:spring-boot-devtools')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.codehaus.groovy:groovy')
	testCompile('org.springframework.boot:spring-boot-starter-test') 
}

dependencyManagement {
	imports { 
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:Brixton.RC2" 
	}
}

sourceSets {
    main {
        groovy {
            srcDirs = ['srv/main/groovy']
        }
        resources {
            srcDirs = ['srv/main/resources']
        }
    }
    test {
        groovy {
            srcDirs = ['srv/test/groovy']
        }
    }
}

ext.springProfilesActive = 'default'

task dev << {
	println 'Running DEVELOPMENT assets'
}
dev.dependsOn 'npm_run_build'
dev.finalizedBy bootRun

task prod << {
	println 'Running PRODUCTION assets'
}
prod.dependsOn 'npm_run_build_prod'
prod.finalizedBy bootRun

if (gradle.startParameter.taskNames.contains("dev")) {
	project.ext.set('springProfilesActive','development')
} else if (gradle.startParameter.taskNames.contains("prod")) {
	project.ext.set('springProfilesActive','production')
} else {
	project.ext.set('springProfilesActive','default')
}

bootRun {
	addResources = true
	environment 'spring.profiles.active', project.springProfilesActive
}

jar.dependsOn 'npm_run_build_prod'